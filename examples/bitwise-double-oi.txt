NAME: Matt Asnes
STATE: q0 qo1 qrb qck qi1 qm qm0 qc qlo1 qlo2 qli1 qli2 qa qr
SIGMA: 0 1
GAMMA: 0 1 o i B
START: q0
ACCEPT: qa
REJECT: qr

DELTA:
; Initial state; rejects on blank, checks next char for 0 or 1.
q0 0 qo1 o R
q0 1 qi1 i R 
q0 B qr
; If initial state found a 0, and now finds a 0, we have a pair
; If initial state found a 0 and now finds a 1, the only non-rejecting
; situations are if we are done; we pass to qrb to check. The chars
; o and i are used as 'marked' 0's and 1's respectively.
qo1 0 qm o R
qo1 1 qrb i R
; if the character to the right is a blank (the case where our
; input was just '01'), we accept. If it was an o or i, we need
; to check that there is no extraneous input at the end.
qrb B qa
qrb o qck o R
qrb i qck i R
; If we are doing a final pass through the oi sequence, reject if
; we see a 0 or 1, and accept only if we get through a series of 
; just o and i chars, and then see a blank.
qck o qck o R
qck i qck i R
qck B qa
; If at the beginning we saw a 1, ensure the next character was a 1.
; Otherwise, reject.
qi1 1 qm i R
; This state is for the middle section of the program, where we want
; to hunt down the '01' segment of the input without changing anything.
qm 1 qm 1 R
qm 0 qm0 0 R
qm0 0 qm 0 R
qm0 1 qc 1 R
; At this state, we have just passed the '01' and need to pass by any
; o or i chars we have previously written. When we find a 0 or 1, just
; replace it with it's marked equivalent and start going back to the
; beginning of the tape.
qc 0 qlo1 o L
qc 1 qli1 i L
qc o qc o R
qc i qc i R
; These four states serve similar purposes; first, we are in either qlo1
; or qli1, going backwards through the o's and i's; we want to ignore 
; them, and when we reach the first 0 or 1, go to a state that is looking
; for them (qlo2 or qli2). These states are going backwards, and if qlo2
; finds an i it rejects; if it finds an o, it loops back to q0. qli2 does
; the same, but for o instead of i and vice versa.
qlo1 0 qlo2 0 L
qlo1 1 qlo2 1 L
qlo1 o qlo1 o L
qlo1 i qlo1 i L
qlo2 0 qlo2 0 L
qlo2 1 qlo2 1 L
qlo2 o q0 o R
qli1 0 qli2 0 L
qli1 1 qli2 1 L
qli1 o qli1 o L
qli1 i qli1 i L
qli2 0 qli2 0 L
qli2 1 qli2 1 L
qli2 i q0 i R

END
